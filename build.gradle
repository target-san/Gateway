// Buildscript config prolog
buildscript {
   	repositories {
       	mavenCentral()
       	maven {
           	name = "forge"
           	url = "http://files.minecraftforge.net/maven"
       	}
       	maven {
           	name = "sonatype"
           	url = "https://oss.sonatype.org/content/repositories/snapshots/"
       	}
   	}
   	dependencies {
       	classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
   	}
}

allprojects {
	// everything should now build in root build folder
	buildDir new File( new File(rootProject.projectDir, "build"), project.name)
	// plugins
	apply plugin: "scala"
	apply plugin: "forge"

	sourceSets.main {
       	def root = project.projectDir // .parentFile
       	java {
           	srcDirs 'src'
       	}
       	scala {
           	srcDirs 'src'
   		}
       	resources {
           	srcDirs 'resources'
   		}
	}
	
	minecraft {
    	version = "1.7.10-10.13.0.1180"
	}
}


subprojects {
	version = "1.0"
	group = 'api'
	archivesBaseName = 'api-' + project.projectDir.name
}

version = "0.1"
group= "targetsan.mcmods.gateway"
archivesBaseName = "gateway"

dependencies {
    compile project('api/NotEnoughItems'), project('api/CoFHLib'), project('api/AppEng2')
}
// Standard thing with setting properties in mcmod.info
processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
